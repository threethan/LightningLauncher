apply plugin: 'com.android.application'
apply plugin: 'com.google.gms.google-services'
apply plugin: 'com.google.firebase.crashlytics'

afterEvaluate {
    tasks.matching { it.name.contains("GoogleServices") && !it.name.contains("store") }*.enabled = false
}

android {
    android.buildFeatures.buildConfig=true

    defaultConfig {
        compileSdk = 36
        minSdkVersion 24
        targetSdkVersion 36
        versionCode 1004
        versionName "10.0.4"
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }
    buildTypes {
        release {
            shrinkResources true
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.debug
        }
        debug {
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    namespace 'com.threethan.launcher'
    lint {
        abortOnError true
        disable 'Accessibility'
    }

    flavorDimensions = ['variant']
    productFlavors {
        sideload {
            dimension 'variant'
            getIsDefault().set(true)
        }
        playstore {
            dimension 'variant'
            applicationIdSuffix '.playstore'
            ext.useGoogleGcm = true
        }
        metastore {
            dimension 'variant'
            applicationIdSuffix '.metastore'
            targetSdkVersion 34
            ext.useGoogleGcm = true
        }
    }
}
dependencies {
    implementation 'androidx.constraintlayout:constraintlayout:2.2.1'

    implementation 'com.google.code.gson:gson:2.13.2'

    implementation 'androidx.datastore:datastore-preferences-rxjava3:1.1.7'
    implementation 'androidx.recyclerview:recyclerview:1.4.0'
    implementation 'androidx.activity:activity:1.11.0'
    implementation "androidx.fragment:fragment:1.8.9"

    // Crashlytics for metastore flavor
    metastoreImplementation platform("com.google.firebase:firebase-bom:34.5.0")
    metastoreImplementation("com.google.firebase:firebase-crashlytics-ndk")
    metastoreImplementation("com.google.firebase:firebase-analytics")
    // Crashlytics for playstore flavor
    playstoreImplementation platform("com.google.firebase:firebase-bom:34.5.0")
    playstoreImplementation("com.google.firebase:firebase-crashlytics-ndk")
    playstoreImplementation("com.google.firebase:firebase-analytics")
}

